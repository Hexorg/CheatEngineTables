<?xml version="1.0" encoding="utf-8"?>
<CheatTable CheatEngineTableVersion="14">
  <CheatEntries>
    <CheatEntry>
      <ID>37</ID>
      <Description>"[X] &lt;== EU3 Devine Wind V5.2 (QMXZ) Script"</Description>
      <Options moHideChildren="1"/>
      <Color>FF0000</Color>
      <VariableType>Auto Assembler Script</VariableType>
      <AssemblerScript>{
===========================================
 Game Title    : Europa Universalis 3 - Divine Wind
 Game Version  : 5.2b (QMXZ) (update to 5.2b by katalambda)
 Game Sponsor  : Torrent
 Proces Name   : eu2game.exe
 Script Version: 1.0
 CE Version    : 6.2
 Release date  : 28-Jan-2013
 Author        : Recifense
 History:
 14-Dec-2010 Release for version 5.0
 11-May-2011 Adapted to version  5.1
 14-May-2011 Adapted to version  5.1 (NHQH)
 17-Oct-2011 Adapted to version  5.1 (BJLT)
 28-Jan-2013 Release for version 5.2b
 Features:
 - Minimum Ducats (62000)
 - Maximum Manpower
 - Minimum Traditions (25%)
   - Army
   - Naval
   - Cultural
 - Minimum Basic Morale (25%)
   - Army
   - Naval
 - Minimum Stability (10)
 - God Mode
   - open fight
   - Siege
 - Quick Battle (applied to the enemy)
   - Minimum Decrease Morale        = 50% (of current one)
   - Minimum Decrease Troop    Size = 50% (of current one)
   - Minimum Decrease Garrison Size = 50% (of current one)
 - Troop Morale in Combat (100%)
 - Quick Ownership of conquered city (by selecting it)
 - Full Garrison on player's cities
 - Minimum Population on player's cities  (2000)
 - Maximum Revolt Risk on player's cities (0)
 - Colonization in 1 day
 - Easy colonization (on selected region)
   - Natives  = 0
   - Ferocity = 0
   - Agressiveness = 0
   - Colonist Chance = 1%
 - Recruitment in 1 day
   - Troops
   - Ships
 - Construction in 1 day
 - Quick Technology Advance (1 level by month)
 - Full Special Units (5)
   - Merchants
   - Colonists
   - Diplomats
   - Missionaries
   - Spies
   - Magistrates
 - Army Movement to adjacent cities in 1 day
 - Useful Pointers
===========================================
}

//=========================================
// Definitions
define(LUDO,eu3game.exe)

//=========================================
[ENABLE]
//=========================================
//LuaCall(CheckVersion())
//=========================================
// Check if script is compatible to this game version
// If false the script will not be loaded

assert(LUDO+3f303f,8b 89 6c 16 00 00 8b 45 08 89 48 04 8b 4d f4 89 10) //_MP    ok
assert(LUDO+1aca53,8b 89 f4 04 00 00 8b 45 08 89 08 5d c2 04 00 ?? ??) //_MDu   ok
assert(LUDO+2c17c7,8b 46 38 39 46 68 ?? ?? 89 46 68 8b 46 68 29 46 38) //_GM    ok
assert(LUDO+2c18a2,89 77 38 5f 5e 8b e5 5d c2 04 00 ?? ?? ?? ?? ?? ??) //_GM1   ok
assert(LUDO+2c18fb,89 4e 3c 5e 8b e5 5d c2 04 00 ?? ?? ?? ?? ?? ?? ??) //_TMor  ok
assert(LUDO+4a8ac0,39 81 d8 04 00 00 68 ?? ?? ?? ?? 8d 4d d8 ?? ?? e8) //_MCity ok
assert(LUDO+4b7fd4,8d 4d bc 89 5e 58 e8 ?? ?? ?? ?? 8b 4e 4c 8b 11 8b) //_MSC   ok
assert(LUDO+21cc05,e8 ?? ?? ?? ?? 53 8b cf e8 ?? ?? ?? ?? 8b 16 8b 0d) //_MCol  ok
assert(LUDO+21d877,e8 ?? ?? ?? ?? 8b 55 f0 52 8b cb e8 ?? ?? ?? ?? 8b) //_MRec  ok
assert(LUDO+21e7cb,e8 ?? ?? ?? ?? 8b 4d e0 56 e8 ?? ?? ?? ?? 8b 75 e4) //_MBCon ok
assert(LUDO+215ec8,8b 09 3b 4d 08 ?? ?? 8b 57 38 2b 57 34 8b 4f 24 c1) //_MRTec ok
assert(LUDO+1b2f79,8b 4d 0c 8b 0c 88 56 8b 75 08 56 e8 ?? ?? ?? ?? 8b) //_MSUni ok
assert(LUDO+2d498f,39 b7 cc 00 00 00 ?? ?? 8b 87 08 01 00 00 8b 08 8b) //_MMov  ok
assert(LUDO+2d4a6c,39 b7 cc 00 00 00 ?? ?? c7 87 d4 00 00 00 64 00 00) //_MMov1 ok
assert(LUDO+2a4032,e8 ?? ?? ?? ?? 8b 55 fc 8b 4d f8 50 57 52 e8 ?? ??) //_MBat  ok
assert(LUDO+164448,89 8f bc 04 00 00 5f 5e 8b e5 5d c2 04 00 ?? ?? ??) //_GM2   ok
assert(LUDO+2c48b9,8b 49 38 8b c4 c7 45 fc 00 00 00 00 89 65 fc 56 89) //_MBat1 ok

//=========================================
alloc(MyCode,4096,LUDO)
//=========================================
// Declaration section
label(_MonPlayer)
label(_BackMP)
label(_MonDucats)
label(_BackMD)
label(_ExitMD)
label(_MonD0)
label(_MonD1)
label(_MonD10)
label(_MonD11)
label(_MonD2)
label(_MonD20)
label(_MonD21)
label(_MonD3)
label(_GodMode)
label(_BackGM)
label(_ExitGM)
label(_GodM0)
label(_GodM1)
label(_TroopMorale)
label(_BackTM)
label(_ExitTM)
label(_GodMode1)
label(_BackGM1)
label(_ExitGM1)
label(_MonCity)
label(_BackMC)
label(_ExitMC)
label(_MonC0)
label(_MonC1)
label(_MonC2)
label(_MonC20)
label(_MonC21)
label(_MonSelCity)
label(_BackMSC)
label(_MonColonization)
label(_BackMCo)
label(_ExitMCo)
label(_MonCo0)
label(_MonRecruitment)
label(_BackMRe)
label(_ExitMRe)
label(_MonBuildingConstruction)
label(_BackMBC)
label(_ExitMBC)
label(_MonResearchTech)
label(_BackMRT)
label(_ExitMRT)
label(_MonSpecialUnits)
label(_BackMSU)
label(_ExitMSU)
label(_MonMovement)
label(_BackMM)
label(_ExitMM)
label(_MonMovement1)
label(_BackMM1)
label(_ExitMM1)
label(_MonBattle)
label(_BackMB)
label(_ExitMB)
label(_GodMode2)
label(_BackGM2)
label(_ExitGM2)
label(_GodM20)
label(_MonBattle1)
label(_BackMB1)
label(_ExitMB1)
label(iPlayerID)
label(pPlayer)
label(pLastOne)
label(pSelCity)
label(pLastCity)
label(pBattleCity)
label(pBattleCity1)
label(iEnableMD)
label(iEnableMMP)
label(iEnableMS)
label(iEnableGM)
label(iEnableTM)
label(iEnableTO)
label(iEnableGA)
label(iEnableQC)
label(iEnableEC)
label(iEnableQR)
label(iEnableQBC)
label(iEnableMRT)
label(iEnableMSU)
label(iEnableMMT)
label(iEnablePOP)
label(iEnableMM)
label(iEnableMMo)
label(iArmyMorale)
label(iNavalMorale)
label(iEnableQB)
label(iEnableMRR)
label(aLast)

//=========================================
// Registering Symbols
registersymbol(MyCode)
registersymbol(iPlayerID)
registersymbol(pPlayer)
registersymbol(pLastOne)
registersymbol(pSelCity)
registersymbol(pLastCity)
registersymbol(pBattleCity)
registersymbol(pBattleCity1)
registersymbol(iEnableMD)
registersymbol(iEnableMMP)
registersymbol(iEnableMS)
registersymbol(iEnableGM)
registersymbol(iEnableTM)
registersymbol(iEnableTO)
registersymbol(iEnableGA)
registersymbol(iEnableQC)
registersymbol(iEnableEC)
registersymbol(iEnableQR)
registersymbol(iEnableQBC)
registersymbol(iEnableMRT)
registersymbol(iEnableMSU)
registersymbol(iEnableMMT)
registersymbol(iEnablePOP)
registersymbol(iEnableMM)
registersymbol(iEnableMMo)
registersymbol(iEnableQB)
registersymbol(iEnableMRR)
registersymbol(aLast)

//=========================================
MyCode:
//========================================= 52b ok
// This code finds out Player ID
_MonPlayer:
 mov ecx,[ecx+0000166c]       // Get player ID

 mov [iPlayerID],ecx          // Save it for further use
 jmp _BackMP                  // Back to main code

//========================================= 52b ok
// Minimum Ducats = 50000, Manpower = Maximum and Stability = 10
_MonDucats:
 mov dword ptr [aLast],'MDUC'

 mov eax,[ecx+1c]             // Get Owner ID

 cmp eax,[iPlayerID]          // Is it the player´s resource?
 jne _ExitMD                  // jump if false

 mov [pPlayer],ecx            // Save pointer for debugging

// Ducats
 cmp dword ptr [iEnableMD],0
 je _MonD0                    // Jump if feature is disabled

 mov eax,#62000000            // Get minimum value (62000)

 cmp eax,[ecx+000004f4]       // Currrent Ducats &gt;= 62000?
 jle _MonD0                   // Jump if true

 mov [ecx+000004f4],eax       // Make Ducats = 62000

// Man Power
_MonD0:
 cmp dword ptr [iEnableMMP],0
 je _MonD1                    // Jump if feature is disabled

 mov eax,[ecx+00000918]       // Get maximum Manpower
 mov [ecx+00000914],eax       // Make Manpower = Max

// Traditions
_MonD1:
 cmp dword ptr [iEnableMMT],0
 je _MonD2                    // Jump if feature is disabled

 mov eax,#250                 // Minimum Tradition

 cmp eax,[ecx+00000c20]       // Army Tradition &gt;= 25%?
 jle _MonD10                  // Jump if true

 mov [ecx+00000c20],eax       // Update tradition

_MonD10:
 cmp eax,[ecx+00000c24]       // Naval Tradition &gt;= 25%?
 jle _MonD11                  // Jump if true

 mov [ecx+00000c24],eax       // Update tradition

_MonD11:
 cmp eax,[ecx+00000c28]       // Cutural Tradition &gt;= 25%?
 jle _MonD2                   // Jump if true

 mov [ecx+00000c28],eax       // Update tradition

// Morale
_MonD2:
 cmp dword ptr [iEnableMMo],0
 je _MonD3                    // Jump if feature is disabled

 mov eax,[ecx+000009f0]       // Get pointer to Army basic Morale?
 cmp dword ptr [eax+00000128],#2500
 jge _MonD20

 mov dword ptr [eax+00000128],#2500

_MonD20:
 mov eax,[eax+00000128]
 mov [iArmyMorale],eax        // Update general army morale

 mov eax,[ecx+00000a38]       // Get pointer to Naval basic Morale?
 cmp dword ptr [eax+00000130],#2500
 jge _MonD21

 mov dword ptr [eax+00000130],#2500

_MonD21:
 mov eax,[eax+00000130]
 mov [iNavalMorale],eax       // Update general Naval morale

// Stability
_MonD3:
 cmp dword ptr [iEnableMS],0
 je _ExitMD                   // Jump if feature is disabled

 mov eax,#10000               // Get minimum value (10)

 cmp eax,[ecx+00000620]       // Stability &gt;= 10 ?
 jle _ExitMD                  // Jump if true

 mov [ecx+00000620],eax       // Stability = 10

_ExitMD:
 mov ecx,[ecx+000004f4]       // Original code (Get Ducats)
 jmp _BackMD                  // Back to main code

//========================================= 52b ok
// God Mode for player's troops on Combat
_GodMode:
 mov dword ptr [aLast],'GMOD'

 mov eax,[esi+64]             // Read troop's owner ID
 cmp eax,[iPlayerID]          // Is it the player's? ...
 jne _GodM1                   // ... Jump if false

 cmp dword ptr [iEnableGM],0
 je _GodM0                    // Jump if feature is disabled

 xor eax,eax
 mov [esi+68],eax             // Value_to_decrease = 0

_GodM0:
 cmp dword ptr [iEnableTM],0  // Morale
 je _ExitGM                   // Jump if feature is disabled

 xor eax,eax
 mov [esi+6c],eax             // Value_to_decrease = 0
 jmp _ExitGM

// Quick Battle
_GodM1:
 cmp dword ptr [iEnableQB],0
 je _ExitGM                   // Jump if feature is disabled

 mov eax,[esi+70]             // Get pArmy

 mov eax,[eax+000000b0]       // Get Army current location (province)
 cmp eax,[pBattleCity]        // Is it the same of last selected battle?
 jne _ExitGM                  // Jump if false

 mov eax,[esi+3c]             // Get troop Morale
 shr eax,1                    // Devide it by 2
 mov [esi+6c],eax             // Make value2decrease = troop_morale/2

 mov eax,[esi+38]             // Get troop size
 shr eax,1                    // Devide it by 2
 cmp eax,[esi+68]             // Value2decrease &gt;= troop_size/2?
 jle _ExitGM                  // Jump if true

 mov [esi+68],eax             // Make value2decrease = troop_size/2

_ExitGM:
 mov eax,[esi+38]             // Original code (get troop size)
 cmp [esi+68],eax             // Original code (compare it to value2decrease)
 jmp _BackGM                  // Back to main code

//========================================= 52b ok
// God Mode for player's troops on Combat
_GodMode1:
 mov dword ptr [aLast],'GMD1'

 cmp dword ptr [iEnableGM],0
 je _ExitGM1                  // Jump if feature is disabled

 push ecx                     // Save ECX
 mov ecx,[edi+64]             // Read troop's owner ID
 cmp ecx,[iPlayerID]          // Is it the player's? ...
 pop ecx                      // Restore ECX
 jne _ExitGM1                 // ... Jump if false

 mov esi,#1000                // Size = 10000

_ExitGM1:
 mov [edi+38],esi             // Original code
 pop edi                      // Original code
 pop esi                      // Original code
 jmp _BackGM1                 // Back to main code

//========================================= 52b ok
// Player's troop morale = 100% on Combat
_TroopMorale:
 mov dword ptr [aLast],'TMOR'

 cmp dword ptr [iEnableTM],0
 je _ExitTM                   // Jump if feature is disabled

 push ebx                     // Save EBX context
 mov ebx,[esi+64]             // Read troop's owner ID
 cmp ebx,[iPlayerID]          // Is it the player's? ...
 pop ebx                      // Restore EBX context
 jne _ExitTM                  // ... Jump if false

 cmp ecx,[esi+3c]             // Next &gt;= current (morale)?
 jge _ExitTM                  // Jump if true

 mov ecx,[esi+3c]             // Next = current

_ExitTM:
 mov [esi+3c],ecx             // Original code
 pop esi                      // Original code
 mov esp,ebp                  // Original code
 jmp _BackTM                  // Back to main code

//========================================= 52b ok
// Ownership, Revolt Risk
// Population = 2000 and Garrison = 1000 (updated when city is selected)
_MonCity:
 mov dword ptr [aLast],'MCIT'

 push eax
 push ebx

 cmp dword ptr [iEnableTO],0  // Take ownership
 je _MonC0                    // Jump if feature is disabled

 mov eax,[ecx+000004e0]       // Get controller iID
 cmp eax,[iPlayerID]          // Is it the player?
 jne _MonC0                   // jump if false

 mov [ecx+000004d8],eax       // Change iOwner
 mov eax,[ecx+000004dc]       // Get controller aID
 mov [ecx+000004d4],eax       // Change aOwner

_MonC0:
 mov [pLastCity],ecx          // Save ptr to city for debugging

 mov eax,[ecx+000004d8]       // Read city's owner ID
 cmp eax,[iPlayerID]          // Is it one of the player´s?
 jne _ExitMC                  // jump if false

// Garrison
 cmp dword ptr [iEnableGA],0
 je _MonC1                    // Jump if feature is disabled

 mov eax,[ecx+00000130]       // Get ptr to general info list
 mov eax,[eax+00000280]       // Get Fort Level
 imul eax,eax,#1000           // Multiply it by 1000

 mov [ecx+000004bc],eax       // Garrison = Maximum

// Population
_MonC1:
 cmp dword ptr [iEnablePOP],0
 je _MonC2                    // Jump if feature is disabled

 mov eax,#2000000
 cmp eax,[ecx+000004b8]       // Population &gt;= 2000?
 jle _MonC2                   // jump if true

 mov [ecx+000004b8],eax       // Population = 2000

// Revolt Risk
_MonC2:
 cmp dword ptr [iEnableMRR],0
 je _ExitMC                   // Jump if feature is disabled

 mov dword ptr [ecx+000004c0],0 // Make revolted pop = 0

 mov ebx,[ecx+00000358]       // Get ptr to list related2nationalism
 mov eax,[ebx+10]             // Get LocalRevoltRisk
 test eax,eax                 // Negative value?
 js _MonC20                   // Jump if true

 mov dword ptr [ebx+08],0     // Make MinimumRevoltRisk = 0
 mov dword ptr [ebx+10],0     // Make LocalRevoltRisk   = 0

_MonC20:
 mov ebx,[ecx+000003a0]       // Get ptr to list related2occupied
 mov eax,[ebx+10]             // Get LocalRevoltRisk
 test eax,eax                 // Negative value?
 js _MonC21                   // Jump if true

 mov dword ptr [ebx+08],0     // Make MinimumRevoltRisk = 0
 mov dword ptr [ebx+10],0     // Make LocalRevoltRisk   = 0

_MonC21:
 mov ebx,[ecx+00000130]       // Get ptr to list related2general
 mov eax,[ebx+10]             // Get LocalRevoltRisk
 test eax,eax                 // Negative value?
 js _ExitMC                   // Jump if true

 mov dword ptr [ebx+08],0     // Make MinimumRevoltRisk = 0
 mov dword ptr [ebx+10],0     // Make LocalRevoltRisk   = 0

_ExitMC:
 pop ebx
 pop eax

 cmp [ecx+000004d8],eax       // Original code (Check iOwner)
 jmp _BackMC                  // Back to main code

//========================================= 52b ok
// Just for debugging
_MonSelCity:
 mov [pSelCity],ebx           // Save pointer for debugging

 lea ecx,[ebp-44]             // Original code
 mov [esi+58],ebx             // Original code (Update Selected City)
 jmp _BackMSC                 // Back to main code

//========================================= 52b tbt
// Colonization in 1 day
// EBX = Colonization Structure OK
// EDI = Colony-to-be OK
_MonColonization:
 mov dword ptr [aLast],'MCOL'

 call LUDO+144bf0             // Original code (Update dates and days)

 mov ecx,[ebx+30]             // Get colonizer ID
 cmp ecx,[iPlayerID]          // Is it the player?
 jne _ExitMCo                 // jump if false

 cmp dword ptr [iEnableQC],0  // Quick Colonization
 je _ExitMCo                  // Jump if feature is disabled

 mov ecx,[ebx+10]             // Get starting date (current date)
 lea ecx,[ecx+18]             // Add 1 day (24 hours)
 mov [ebx+14],ecx             // Save finishing date (just 1 day)
 mov dword ptr [ebx+18],1     // Make remaining_days = 1

_MonCo0:
 cmp dword ptr [iEnableEC],0  // Easy Colonization
 je _ExitMCo                  // Jump if feature is disabled

 mov dword ptr [edi+00000524],0  // Natives = 0
 mov dword ptr [edi+00000528],0  // Ferocity = 0
 mov dword ptr [edi+0000052c],0  // Agressiveness = 0

 mov ecx,[edi+000002c8]             // Get pointer to agressiveness factor list
 mov dword ptr [ecx+000000f8],#100  // LocalColonistChance = +1%
 mov dword ptr [ecx+00000338],#100  // LocalColonialGrowth = +1%

_ExitMCo:
 jmp _BackMCo                 // Back to main code

//========================================= 52b ok
// Recruitment/Mercenaries or Ship production in 1 day
// EBX = Recruitment Structure or Ship Production Structure
_MonRecruitment:
 mov dword ptr [aLast],'MREC'

 call LUDO+144bf0             // Original code (Update dates and days)

 mov ecx,[ebx+30]             // Get recruitment ID
 cmp ecx,[iPlayerID]          // Is it the player?
 jne _ExitMRe                 // jump if false

 cmp dword ptr [iEnableQR],0  // Quick Recruitment
 je _ExitMRe                  // Jump if feature is disabled

 mov ecx,[ebx+10]             // Get starting date (current date)
 lea ecx,[ecx+18]             // Add 1 day (24 hours)
 mov [ebx+14],ecx             // Save finishing date (just 1 day)
 mov dword ptr [ebx+18],1     // Make remaining_days = 1

_ExitMRe:
 jmp _BackMRe                 // Back to main code

//========================================= 52b ok
// Construction of Buildings in 1 day
// ESI = Construction Structure, EDI = pOwnerInfo
_MonBuildingConstruction:
 mov dword ptr [aLast],'MBCo'

 call LUDO+144bf0             // Original code (Update dates and days)

 mov ecx,[edi+1c]             // Get player ID
 cmp ecx,[iPlayerID]          // Is it the player?
 jne _ExitMBC                 // jump if false

 cmp dword ptr [iEnableQBC],0 // Quick Construction
 je _ExitMBC                  // Jump if feature is disabled

 mov ecx,[esi+10]             // Get starting date (current date)
 lea ecx,[ecx+18]             // Add 1 day (24 hours)
 mov [esi+14],ecx             // Save finishing date (just 1 day)
 mov dword ptr [esi+18],1     // Make remaining_days = 1

_ExitMBC:
 jmp _BackMBC                 // Back to main code

//========================================= 52b ok
// Quick Technology Advance
_MonResearchTech:
 mov dword ptr [aLast],'MRTE'

 push edx                     // Save EDX context

 cmp dword ptr [iEnableMRT],0
 je _ExitMRT                  // Jump if feature is disabled

 mov edx,[esi+000000cc]       // Get pPlayer
 mov edx,[edx+1c]             // Get OwnerID
 cmp edx,[iPlayerID]          // Is it a player research?
 jne _ExitMRT                 // Jump if false

 mov edx,[ebp+08]             // Get value to complete research
 mov [ecx],edx                // make it complete

_ExitMRT:
 pop edx                      // Restore EDX context

 mov ecx,[ecx]                // Original code (get research value)
 cmp ecx,[ebp+08]             // Original code (check if complete)
 jmp _BackMRT                 // Back to main code

//========================================= 52b ok
// Special Unit = Maximum (5)
// Merchants, colonists, diplomats, missionaries, spies, magistrates
_MonSpecialUnits:
 mov dword ptr [aLast],'MSUN'

 push edx                     // Save EDX context
 mov edx,ecx                  // EDX = Player Structure

 mov ecx,[ebp+0c]             // Original Code (Get Special Unit Index)
 mov ecx,[eax+ecx*4]          // Original Code (Get pointer to Special Unit)

 cmp dword ptr [iEnableMSU],0
 je _ExitMSU                  // Jump if feature is disabled

 mov edx,[edx+1c]             // Get OwnerID
 cmp edx,[iPlayerID]          // Is it the player?
 jne _ExitMSU                 // Jump if false

 mov dword ptr [ecx+08],#5000 // Make available units = maximum

_ExitMSU:
 pop edx                      // Restore EDX context
 jmp _BackMSU                 // Back to main code

//========================================= 52b ok
// Troop Movement between adjacent province in 1 Day
_MonMovement:
 mov dword ptr [aLast],'MMOV'

 push edx                     // Save EDX context

 cmp dword ptr [iEnableMM],0
 je _ExitMM                   // Jump if feature is disabled

 mov edx,[edi+000000a0]       // Get OwnerID
 cmp edx,[iPlayerID]          // Is it a player research?
 jne _ExitMM                  // Jump if false

 cmp esi,[edi+000000cc]       // Movement progress = 100% ?
 jle _ExitMM                  // Jump if false

 mov [edi+000000cc],esi       // Make progress = 100%

_ExitMM:
 pop edx                      // Restore EDX context

 cmp [edi+000000cc],esi       // Original code
 jmp _BackMM                  // Back to main code

//========================================= 52b ok
// Troop Movement between adjacent province in 1 Day
_MonMovement1:
 mov dword ptr [aLast],'MMV1'

 push edx                     // Save EDX context

 cmp dword ptr [iEnableMM],0
 je _ExitMM1                  // Jump if feature is disabled

 mov edx,[edi+000000a0]       // Get OwnerID
 cmp edx,[iPlayerID]          // Is it a player research?
 jne _ExitMM1                 // Jump if false

 cmp esi,[edi+000000cc]       // Movement progress = 100% ?
 jle _ExitMM1                 // Jump if false

 mov [edi+000000cc],esi       // Make progress = 100%

_ExitMM1:
 pop edx                      // Restore EDX context

 cmp [edi+000000cc],esi       // Original code
 jmp _BackMM1                 // Back to main code

//========================================= 52b suspect
// This identifies the province where the battle takes place (Not siege)
_MonBattle:
 mov dword ptr [aLast],'MBAT'

 push edx                     // Save EDX context

 mov edx,[esi+64]             // Get ownerID (troop)
 cmp edx,[iPlayerID]          // Is it the player?
 je _ExitMB                   // Jump if true

 mov edx,[esi+70]             // Get enemy's army address
 mov edx,[edx+000000b0]       // Get ptr to current province (city)
 mov [pBattleCity],edx        // Save pointer for further use

_ExitMB:
 pop edx                      // Restore EDX context

 call LUDO+0552f0             // Original code
 jmp _BackMB                  // Back to main code

//========================================= 52b
// God Mode for Garrison soldiers (siege)
_GodMode2:
 mov dword ptr [aLast],'GMD2'

 mov esi,[edi+000004d8]       // Get OwnerID
 cmp esi,[iPlayerID]          // Is it the player?
 je _GodM20                   // Jump if true

 cmp dword ptr [iEnableQB],0
 je _ExitGM2                  // Jump if feature is disabled

 cmp edi,[pBattleCity1]       // Is it the enemy city of last select siege?
 jne _ExitGM2                 // Jump if false

 mov esi,[edi+000004bc]       // Get Garrison troop_size
 shr esi,2                    // Devide it by 4
 cmp ecx,esi                  // new value &lt;= troop_size/4?
 jle _ExitGM2                 // Jump if true

 mov ecx,esi                  // Make new value = troop_size/4
 jmp _ExitGM2

_GodM20:
 cmp dword ptr [iEnableGM],0
 je _ExitGM2                  // Jump if feature is disabled

 cmp ecx,[edi+000004bc]       // New value &gt;= current value?
 jge _ExitGM2                 // Jump if true

 mov ecx,[edi+000004bc]       // Make New value = current value

_ExitGM2:
 mov [edi+000004bc],ecx       // Original code (update garrison soldiers)
 jmp _BackGM2                 // Back to main code

//========================================= 52b **
// This identifies the province where the battle takes place (Siege)
_MonBattle1:
 mov dword ptr [aLast],'MBA1'

 push edx                     // Save EDX context

 mov edx,[ecx+64]             // Get ownerID (troop)
 cmp edx,[iPlayerID]          // Is it the player?
 jne _ExitMB1                 // Jump if false

 mov edx,[ecx+70]             // Get ptr to army
 mov edx,[edx+000000b0]       // Get ptr to current province (city)

 mov eax,[edx+000004d8]       // Get OwnerID
 cmp eax,[iPlayerID]          // Is it the player?
 je _ExitMB1                  // Jump if true

 mov [pBattleCity1],edx       // Save it for further use

_ExitMB1:
 pop edx                      // Restore EDX context

 mov ecx,[ecx+38]             // Original code (get troop size)
 mov eax,esp                  // Original code
 jmp _BackMB1                 // Back to main code

//=========================================
 db '=========================&gt;'
 db 'CE6.2 Script by Recifense 012813'
//=========================================
// Variables
iEnableMD:
 dd 0
iEnableMMP:
 dd 0
iEnableGM:
 dd 0
iEnableTM:
 dd 0
iEnableTO:
 dd 0
iEnableMS:
 dd 0
iEnableMRT:
 dd 0
iEnableGA:
 dd 0
iEnableQC:
 dd 0
iEnableEC:
 dd 0
iEnableQR:
 dd 0
iEnableQBC:
 dd 0
iEnableMSU:
 dd 0
iEnableMMT:
 dd 0
iEnablePOP:
 dd 0
iEnableMM:
 dd 0
iEnableMMo:
 dd 0
iEnableQB:
 dd 0
iEnableMRR:
 dd 0
iPlayerID:
 dd 0
pPlayer:
 dd 0
pLastOne:
 dd 0
pSelCity:
 dd 0
pLastCity:
 dd 0
pBattleCity:
 dd 0
pBattleCity1:
 dd 0
iArmyMorale:
 dd #2500
iNavalMorale:
 dd #2500
aLast:
 db 'CE62'

//=========================================
// Hacking Points
//eu3game.exe+2bcd7f:
//eu3game.exe+2fffdf: //BJLT
LUDO+3f303f: //ok
 jmp _MonPlayer
 nop
_BackMP:

//eu3game.exe+0b7363:
//eu3game.exe+0bba53: //BJLT
LUDO+1aca53: //ok
 jmp _MonDucats
 nop
_BackMD:

//eu3game.exe+1cea27:
//eu3game.exe+1d3a47: //BJLT
LUDO+2c17c7: //ok
 jmp _GodMode
 nop
_BackGM:

//eu3game.exe+1ceb02:
//eu3game.exe+1d3b22: //BJLT
LUDO+2c18a2: //ok
 jmp _GodMode1
_BackGM1:

//eu3game.exe+1ceb5b:
//eu3game.exe+1d3b7b: //BJLT
LUDO+2c18fb: //ok
 jmp _TroopMorale
 nop
_BackTM:

//eu3game.exe+3b0d30:
//eu3game.exe+3b2fd0: //BJLT
LUDO+4a8ac0: //ok
 jmp _MonCity
 nop
_BackMC:

//eu3game.exe+3c0214:
//eu3game.exe+3c24e4: //BJLT
LUDO+4b7fd4: //ok
 jmp _MonSelCity
 nop
_BackMSC:

//eu3game.exe+127635:
//eu3game.exe+12bdf5: //BJLT
LUDO+21cc05: //ok
 jmp _MonColonization
_BackMCo:

//eu3game.exe+1282a7:
//eu3game.exe+12ca67: //BJLT
LUDO+21d877: //ok
 jmp _MonRecruitment
_BackMRe:

//eu3game.exe+1291fb:
//eu3game.exe+12d9bb: //BJLT
LUDO+21e7cb: //ok
 jmp _MonBuildingConstruction
_BackMBC:

//eu3game.exe+1208d8:
//eu3game.exe+125118: //BJLT
LUDO+215ec8: //ok
 jmp _MonResearchTech
_BackMRT:

//eu3game.exe+0bd8e9:
//eu3game.exe+0c1f19: //BJLT
LUDO+1b2f79: //ok
 jmp _MonSpecialUnits
 nop
_BackMSU:

//eu3game.exe+1e117a:
//eu3game.exe+1e6176: //BJLT
LUDO+2d498f: //ok
 jmp _MonMovement
 nop
_BackMM:

//eu3game.exe+1e1250:
//eu3game.exe+1e6253: //BJLT
LUDO+2d4a6c: //ok
 jmp _MonMovement1
 nop
_BackMM1:

//eu3game.exe+1ae732:
//eu3game.exe+1b5292: //BJLT
LUDO+2a4032: //ok
 jmp _MonBattle
_BackMB:

//eu3game.exe+06efd8:
//eu3game.exe+070a48: //BJLT
LUDO+164448: //ok
 jmp _GodMode2
 nop
_BackGM2:

//eu3game.exe+1d1ae9:
//eu3game.exe+1d6b69: //BJLT
LUDO+2c48b9: //ok
 jmp _MonBattle1
_BackMB1:

//=========================================
// Script for Restoring Original Codes
[DISABLE]
//eu3game.exe+2bcd7f:
//eu3game.exe+2fffdf: //BJLT
LUDO+3f303f: //ok
// mov ecx,[ecx+0000166C]
 db 8B 89 6C 16 00 00

//eu3game.exe+0b7363:
//eu3game.exe+0bba53: //BJLT
LUDO+1aca53: //ok
// mov ecx,[ecx+000004f4]
 db 8B 89 f4 04 00 00

//eu3game.exe+1cea27:
//eu3game.exe+1d3a47: //BJLT
LUDO+2c17c7: //ok
// mov eax,[esi+38]
// cmp [esi+68],eax
 db 8B 46 38 39 46 68

//eu3game.exe+1ceb02:
//eu3game.exe+1d3b22: //BJLT
LUDO+2c18a2: //ok
// mov [edi+38],esi
// pop edi
// pop esi
 db 89 77 38 5f 5e

//eu3game.exe+1ceb5b:
//eu3game.exe+1d3b7b: //BJLT
LUDO+2c18fb: //ok
// mov [esi+3c],ecx
// pop esi
// mov esp,ebp
 db 89 4e 3c 5e 8b e5

//eu3game.exe+3b0d30:
//eu3game.exe+3b2fd0: //BJLT
LUDO+4a8ac0: //ok
// cmp [ecx+000004d8],eax
 db 39 81 d8 04 00 00

//eu3game.exe+3c0214:
//eu3game.exe+3c24e4: //BJLT
LUDO+4b7fd4: //ok
// lea ecx,[ebp-44]
// mov [esi+58],ebx
 db 8d 4d bc 89 5e 58

//eu3game.exe+127635:
//eu3game.exe+12bdf5: //BJLT
LUDO+21cc05: //ok
 call LUDO+144bf0

//eu3game.exe+1282a7:
//eu3game.exe+12ca67: //BJLT
LUDO+21d877: //ok
 call LUDO+144bf0

//eu3game.exe+1291fb:
//eu3game.exe+12d9bb: //BJLT
LUDO+21e7cb: //ok
 call LUDO+144bf0

//eu3game.exe+1208d8:
//eu3game.exe+125118: //BJLT
LUDO+215ec8: //ok
// mov ecx,[ecx]
// cmp ecx,[ebp+08]
 db 8b 09 3b 4d 08

//eu3game.exe+0bd8e9:
//eu3game.exe+0c1f19: //BJLT
LUDO+1b2f79: //ok
// mov ecx,[ebp+0c]
// mov ecx,[eax+ecx*4]
 db 8b 4d 0c 8b 0c 88

//eu3game.exe+1e117a:
//eu3game.exe+1e6176: //BJLT
LUDO+2d498f: //ok
// cmp [edi+000000CC],esi
 db 39 b7 cc 00 00 00

//eu3game.exe+1e1250:
//eu3game.exe+1e6253: //BJLT
LUDO+2d4a6c: //ok
// cmp [edi+000000CC],esi
 db 39 b7 cc 00 00 00

//eu3game.exe+1ae732:
//eu3game.exe+1b5292: //BJLT
LUDO+2a4032: //ok
 call LUDO+0552f0

//eu3game.exe+06efd8:
//eu3game.exe+070a48: //BJLT
LUDO+164448: //ok
// mov [edi+000004bc],ecx
 db 89 8f bc 04 00 00

//eu3game.exe+1d1ae9:
//eu3game.exe+1d6b69: //BJLT
LUDO+2c48b9: //ok
// mov ecx,[ecx+38]
// mov eax,esp
 db 8b 49 38 8b c4

//=========================================
// Unregistering Symbols
unregistersymbol(MyCode)
unregistersymbol(iPlayerID)
unregistersymbol(pPlayer)
unregistersymbol(pLastOne)
unregistersymbol(pSelCity)
unregistersymbol(pLastCity)
unregistersymbol(pBattleCity)
unregistersymbol(pBattleCity1)
unregistersymbol(iEnableMD)
unregistersymbol(iEnableMMP)
unregistersymbol(iEnableMS)
unregistersymbol(iEnableGM)
unregistersymbol(iEnableTM)
unregistersymbol(iEnableTO)
unregistersymbol(iEnableGA)
unregistersymbol(iEnableQC)
unregistersymbol(iEnableEC)
unregistersymbol(iEnableQR)
unregistersymbol(iEnableQBC)
unregistersymbol(iEnableMRT)
unregistersymbol(iEnableMSU)
unregistersymbol(iEnableMMT)
unregistersymbol(iEnablePOP)
unregistersymbol(iEnableMM)
unregistersymbol(iEnableMMo)
unregistersymbol(iEnableQB)
unregistersymbol(iEnableMRR)
unregistersymbol(aLast)

//=========================================
dealloc(MyCode)
//============= Scripts End ===============

// ****************************************
// NOTES
// ****************************************
{
}


</AssemblerScript>
      <CheatEntries>
        <CheatEntry>
          <ID>1</ID>
          <Description>"Enable/Disable Minimum Ducats"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMD</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>38</ID>
          <Description>"Enable/Disable Maximum Man Power"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMMP</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>3</ID>
          <Description>"Enable/Disable Minimum Traditions"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMMT</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>4</ID>
          <Description>"Enable/Disable Minimum Basic Army/Naval Morale"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMMo</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>5</ID>
          <Description>"Enable/Disable Minimum Stability"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMS</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>6</ID>
          <Description>"Enable/Disable God Mode"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableGM</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>7</ID>
          <Description>"Enable/Disable Troop Morale"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableTM</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>8</ID>
          <Description>"Enable/Disable Quick Battle"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableQB</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>9</ID>
          <Description>"Enable/Disable Province Ownership"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableTO</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>10</ID>
          <Description>"Enable/Disable Province Full Garrison"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableGA</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>11</ID>
          <Description>"Enable/Disable Minimum Province Population"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnablePOP</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>12</ID>
          <Description>"Enable/Disable Minimum Province Revolt Risk"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMRR</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>13</ID>
          <Description>"Enable/Disable Colonization in 1Day"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableQC</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>14</ID>
          <Description>"Enable/Disable Easy Colonization"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableEC</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>15</ID>
          <Description>"Enable/Disable Recruitment in 1Day"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableQR</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>16</ID>
          <Description>"Enable/Disable Building Construction in 1Day"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableQBC</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>17</ID>
          <Description>"Enable/Disable Quick Technology Research"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMRT</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>18</ID>
          <Description>"Enable/Disable Full of Special Units"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMSU</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>19</ID>
          <Description>"Enable/Disable Army/Fleet Travel in 1 Day"</Description>
          <Color>008000</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>iEnableMM</Address>
        </CheatEntry>
        <CheatEntry>
          <ID>20</ID>
          <Description>"Current Player Prestigy (*1000)"</Description>
          <Color>0000FF</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>pPlayer</Address>
          <Offsets>
            <Offset>700</Offset>
          </Offsets>
        </CheatEntry>
        <CheatEntry>
          <ID>21</ID>
          <Description>"Current Player Legitimacy (*10)"</Description>
          <Color>0000FF</Color>
          <VariableType>4 Bytes</VariableType>
          <Address>pPlayer</Address>
          <Offsets>
            <Offset>C7C</Offset>
          </Offsets>
        </CheatEntry>
      </CheatEntries>
    </CheatEntry>
  </CheatEntries>
  <UserdefinedSymbols/>
  <Comments>===========================================
 Game Title    : Europa Universalis 3 - Divine Wind
 Game Version  : 5.2b (QMXZ) (update to 5.2b by katalambda)
 Game Sponsor  : Torrent
 Proces Name   : eu2game.exe
 Script Version: 1.0
 CE Version    : 6.2
 Release date  : 29-Jan-2013
 Author        : Recifense
 History:
 14-Dec-2010 Release for version 5.0
 11-May-2011 Adapted to version  5.1
 14-May-2011 Adapted to version  5.1 (NHQH)
 17-Oct-2011 Adapted to version  5.1 (BJLT)
 28-Jan-2013 Release for version 5.2b
 Features:
 - Minimum Ducats (62000)
 - Maximum Manpower
 - Minimum Traditions (25%)
   - Army
   - Naval
   - Cultural
 - Minimum Basic Morale (25%)
   - Army
   - Naval
 - Minimum Stability (10)
 - God Mode
   - open fight
   - Siege
 - Quick Battle (applied to the enemy)
   - Minimum Decrease Morale        = 50% (of current one)
   - Minimum Decrease Troop    Size = 50% (of current one)
   - Minimum Decrease Garrison Size = 50% (of current one)
 - Troop Morale in Combat (100%)
 - Quick Ownership of conquered city (by selecting it)
 - Full Garrison on player's cities
 - Minimum Population on player's cities  (2000)
 - Maximum Revolt Risk on player's cities (0)
 - Colonization in 1 day
 - Easy colonization (on selected region)
   - Natives  = 0
   - Ferocity = 0
   - Agressiveness = 0
   - Colonist Chance = 1%
 - Recruitment in 1 day
   - Troops
   - Ships
 - Construction in 1 day
 - Quick Technology Advance (1 level by month)
 - Full Special Units (5)
   - Merchants
   - Colonists
   - Diplomats
   - Missionaries
   - Spies
   - Magistrates
 - Army Movement to adjacent cities in 1 day
 - Useful Pointers
===========================================
All feature are disabled by default (value = 0). In order to enable any of them, just change its value to 1.
===========================================
You can create hotkeys for each cheats.
===========================================
Cheers!
</Comments>
</CheatTable>
